plugins {
    kotlin("jvm")
    id("info.solidsoft.pitest")
}

kotlin {
    jvmToolchain(22)
}

tasks.test {
    useJUnitPlatform()
    jvmArgs("--enable-native-access=ALL-UNNAMED")
    systemProperty("kotest.framework.classpath.scanning.config.disable", "true")
    exclude("**/seccomp/**")
    exclude("**/landlock/**")
    exclude("**/SecurityPolicyTest*")
    exclude("**/ProcessContainmentInheritanceTest*")
    exclude("**/ContainedExecutorsTest*")
    exclude("**/VirtualThreadGuardrailTest*")
}

val integrationTest =
    tasks.register<Test>("integrationTest") {
        group = "verification"
        description = "Runs integration tests that install seccomp or Landlock filters, forcing a fresh JVM for each test."
        useJUnitPlatform()
        jvmArgs("--enable-native-access=ALL-UNNAMED")
        systemProperty("kotest.framework.classpath.scanning.config.disable", "true")
        forkEvery = 1
        include("**/seccomp/**")
        include("**/landlock/**")
        include("**/SecurityPolicyTest*")
        include("**/ProcessContainmentInheritanceTest*")
        include("**/ContainedExecutorsTest*")
        include("**/VirtualThreadGuardrailTest*")
    }

tasks.check {
    dependsOn(integrationTest)
}
dependencies {
    testImplementation(kotlin("test"))
    testImplementation(libs.junit.jupiter.api)
    testImplementation(libs.junit.jupiter.params)
    testImplementation(libs.archunit.junit5)
    testImplementation(libs.kotest.assertions)
    testImplementation(libs.kotest.property)
    testImplementation(libs.kotest.runner)
    testImplementation(libs.kotlinxCoroutines)
    testRuntimeOnly(libs.junit.jupiter.engine)
    testRuntimeOnly(libs.slf4j.nop)
}

publishing {
    publications {
        create<MavenPublication>("mavenJava") {
            from(components["java"])
        }
    }
}

tasks.withType<Test> {
    testLogging {
        showStandardStreams = true
    }
}

tasks.register<JavaExec>("runScratch") {
    group = "application"
    classpath = sourceSets["test"].runtimeClasspath
    mainClass.set("io.mazewall.Scratch")
    jvmArgs("--enable-native-access=ALL-UNNAMED")
}

pitest {
    junit5PluginVersion.set("1.2.1")

    // Minimal target set for SbobParser only
    targetClasses.set(setOf("io.mazewall.SbobParser*"))
    targetTests.set(setOf("io.mazewall.SbobParserTest"))

    jvmArgs.set(listOf("--enable-native-access=ALL-UNNAMED"))

    threads.set(1)
}
